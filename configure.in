dnl Process this file with autoconf to produce a configure script.
AC_INIT(cli/cmd_attach.cc)
AM_CONFIG_HEADER(config.h)

dnl option for setting exdbm path prefix
dnl exdbm must be lowercase in --with option. sorry.
dnl this check must be done very early otherwise internal
dnl configure shell variables are set too late (CPPFLAGS)
AC_ARG_WITH(exdbm,
    [  --with-exdbm=PREFIX     where is exdbm installed],
    [CPPFLAGS="$CPPFLAGS -I$with_exdbm/include"
    LDFLAGS="$LDFLAGS -L$with_exdbm/lib"])

AC_CANONICAL_SYSTEM

GPSIM_MAJOR_VERSION=0
GPSIM_MINOR_VERSION=20
GPSIM_MICRO_VERSION=4
GPSIM_VERSION=$GPSIM_MAJOR_VERSION.$GPSIM_MINOR_VERSION.$GPSIM_MICRO_VERSION

AM_INIT_AUTOMAKE(gpsim, $GPSIM_VERSION)

AC_SUBST(GPSIM_MAJOR_VERSION)
AC_SUBST(GPSIM_MINOR_VERSION)
AC_SUBST(GPSIM_MICRO_VERSION)
AC_SUBST(GPSIM_VERSION)

dnl Checks for programs.
AC_PROG_YACC

dnl compilers
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP

dnl AC_PROG_LEX
AM_PROG_LEX
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl library tools
AM_PROG_LIBTOOL

dnl --disable-gui : turn off gui support (cli only)
dnl    The default is to have the gui. 

AC_ARG_ENABLE(gui,
     [  --disable-gui           Only use the cli and not the gui],
     [case "${enableval}" in
       yes) use_gui=yes ;;
       no)  use_gui=no ;;
       *) AC_MSG_ERROR(bad value ${enableval} for --disable-gui) ;;
     esac],[use_gui=yes])

if test "$use_gui" = "no"; then
	echo disabling gui support
else
	echo enabling gui support
	[
	 use_gui=yes
	]
	AC_DEFINE(HAVE_GUI)
fi



dnl Checks for libraries.
dnl Replace `main' with a function in -lfl:
dnl AC_CHECK_LIB(fl, main)
dnl Replace `main' with a function in -lreadline:
wi_LIB_READLINE
dnl AC_CHECK_LIB(readline, readline)
dnl AC_CHECK_LIB(ncurses, main)

dnl Replace `main' with a function in -ltermcap:
dnl AC_CHECK_LIB(termcap, main)

if test "$use_gui" = "no"; then

  dnl glib checks
  AC_PATH_PROG(GLIBCONFIG, glib-config, no, ${PATH})

  if test $GLIBCONFIG = "no"; then
    AC_MSG_ERROR(Cannot find glib-config: glib is needed even for non-gui version.)
  fi

  LDEXTRA=
  CFEXTRA=
  GTK=
  GDK=
  GLIB=
  X_LDFLAGS=`$GLIBCONFIG --libs`
  X_CFLAGS=`$GLIBCONFIG --cflags`
  Y_LDFLAGS=
  Y_CFLAGS=

else
  dnl gtk checks
  AC_PATH_PROG(GTKCONFIG, gtk-config, no, ${PATH})

  if test $GTKCONFIG = "no"; then
    AC_MSG_ERROR(Cannot find gtk-config)
  fi

  AC_PATH_PROG(GTKEXTRACONFIG, gtkextra-config, no, ${PATH})

  if test $GTKEXTRACONFIG = "no"; then
    AC_MSG_ERROR(Cannot find gtkextra-config)
  fi

  LDEXTRA=
  CFEXTRA=
  GTK=
  GDK=
  GLIB=
  X_LDFLAGS=`$GTKCONFIG --libs`
  X_CFLAGS=`$GTKCONFIG --cflags`
  Y_LDFLAGS=`$GTKEXTRACONFIG --libs`
  Y_CFLAGS=`$GTKEXTRACONFIG --cflags`

  dnl check that the eXdbm header can be found
  AC_CHECK_HEADER(eXdbm/eXdbm.h,,[
  AC_MSG_ERROR([eXdbm is needed. please use --with-exdbm option.
  see ./configure --help for instructions])
  ])

  dnl check that the eXdbm library can be linked, and add the link command
  dnl to the LIBS variable.
  AC_CHECK_LIB(eXdbm,eXdbmInit,,[
  AC_MSG_ERROR([eXdbm is needed. please use --with-exdbm option.
  see ./configure --help for instructions])
  ])

fi

AC_SUBST(Y_CFLAGS)
AC_SUBST(Y_LDFLAGS)
AC_SUBST(X_CFLAGS)
AC_SUBST(X_LDFLAGS)
AC_SUBST(GTK)
AC_SUBST(GDK)
AC_SUBST(GLIB)
AC_SUBST(LIBREADLINE)

dnl Checks for header files.
AC_HEADER_STDC
dnl AC_CHECK_HEADERS(malloc.h sys/file.h unistd.h)

dnl move the alloca check here since it doesn't produce valid C++
dnl code either
AC_FUNC_ALLOCA

dnl Set lang to C++ after checking for C Gtk; checks after this will use CXX 
dnl  instead of CC
AC_LANG_CPLUSPLUS


dnl Checks for typedefs, structures, and compiler characteristics.
dnl the const and inline checks produce illegal C++ code, skip them,
dnl C++ is required to have both, a working const and a working inline
dnl AC_C_CONST
dnl AC_C_INLINE
dnl AC_TYPE_SIZE_T

dnl Checks for library functions.
dnl AC_FUNC_ALLOCA
AC_TYPE_SIGNAL

AC_CHECK_FUNCS(strdup strstr strtod strtol strtoul)

AC_OUTPUT( src/Makefile cli/Makefile Makefile \
	gui/Makefile  xpms/Makefile \
	gpsim/Makefile examples/Makefile \
	examples/12bit/Makefile examples/14bit/Makefile examples/16bit/Makefile \
	doc/Makefile )

AC_MSG_RESULT([

gpsim-$GPSIM_VERSION is now configured for $canonical_host_type

  Source directory:     $srcdir
  Installation prefix:  $prefix
  C compiler:           $CC $CPPFLAGS $XTRA_CFLAGS $CFLAGS
  C++ compiler:         $CXX $CPPFLAGS $XTRA_CXXFLAGS $CXXFLAGS

  gui:			$use_gui

])
